PARSER_BEGIN(SyntaxChecker)

public class SyntaxChecker {
    public static void main(String[] args) {
        try {
            new SyntaxChecker(new java.io.FileReader(args[0])).SCRIPT();
            System.out.println("Syntax is okay");
        } catch (Throwable e) {
            // Catching Throwable is ugly but JavaCC throws Error objects!
            System.out.println("Syntax check failed: " + e.getMessage());
        }
    }
}

PARSER_END(SyntaxChecker)

SKIP: { " " | "\t"
    | <"<(-_-)>" (~["\n","\r"])* ("\n"|"\r")>
}

SKIP : {
    "<(-.-)>" : WithinComment
}

<WithinComment> SKIP :
{
    "<(-.-)>" : DEFAULT
}

<WithinComment> MORE :
{
    <~[]>
}

TOKEN: {
    "(" | ")" | "{" | "}" | "+" | "*" | ":=" | "|" | "&" | "," | ":" | "."
    | <IF: ("if")>
	| <TO: ("to")>
	| <BY: ("by")>
	| <AND: ("and")>
	| <HMM: ("hmm")>
	| <HMMQ: ("hmm?")>
	| <IS: ("is?")>
	| <ELSE: ("else")>
	| <FROM: ("from")>
	| <DOES: ("does")>
	| <RUNS: ("runs")>
	| <WHILE: ("while")>
	| <BEGIN: ("begin")>
	| <BEGINS: ("begins")>
	| <GIVEN: ("given")>
	| <GIVES: ("gives")>
	| <TIMES: ("times")>
	| <BECOME: ("become")>
	| <BECOMES: ("becomes")>
	| <NOTHING: ("nothing")>
	| <THROUGH: ("through")>
	| <AS: ("as")>
	| <TRAINING: ("training")>
	| <MUST: ("must")>
	| <BE: ("be")>
	| <YOU: ("you")>
	| <PRINT: ("print")>
	| <GIVEBACK: ("give back")>
	| <ID: ((["a"-"z"] | ["A"-"Z"])+(["-"] | ["_"] | ["a"-"z"] | ["A"-"Z"] | ["0"-"9"])*)>
    | <NUMLIT: (("0x" | "0b" | "0o")? (["0"-"9"])+("."(["0"-"9"])*)?)>
    | <STRLIT: ("\"" (~["\""] | ("\\\""))* "\"") >
    | <BR: ("\n" | "\r" | "\r\n" )>
	
}


void SCRIPT(): {} { ((<BR>)* STMT())+ (<BR>)* <EOF> }
void STMT(): {} { 
     LOOKAHEAD(BLOCK() <WHILE>) WHILELOOP()
   | LOOKAHEAD(BLOCK() <AS>) FORLOOP()
   | LOOKAHEAD(BLOCK() <IF>) CONDITIONAL()
   | LOOKAHEAD(BLOCK() EXP()) TIMESLOOP()
   | LOOKAHEAD(BLOCK() <GIVEN> ARGS()) ANONFUN()
   | LOOKAHEAD(BLOCK() <GIVEN> PARAMS() "," <ID> <DOES>) PROCDEC()
   | LOOKAHEAD(BLOCK() <GIVEN> PARAMS() "," <ID> <GIVES>) FUNDEC()   
   | LOOKAHEAD(EXP() <YOU> <PRINT>) PRINTSTMT()
   | LOOKAHEAD(EXP() "," <ID> <MUST> <BE>) CONSTDEC()
   | LOOKAHEAD(<FROM> | (EXP() (<AND> EXP())*  "," <ID> ( <AND> <ID>)* (<BEGIN> | <BEGINS>))) VARDEC()
   | LOOKAHEAD(EXP() (<AND> EXP())* "," <ID> ( <AND> <ID>)* (<BECOME> | <BECOMES>)) ASSIGNMENT()
   | LOOKAHEAD((<ID> ".") | ("(" ARGS())) FUNCALL()
   | LOOKAHEAD(<GIVEBACK>) RETURNSTMT()
   | LOOKAHEAD(FUNCALL()) PROCCALL()
   | EXP()
}

void DEC(): {} { LOOKAHEAD(4) VARDEC() | LOOKAHEAD(3) CONSTDEC() | LOOKAHEAD(3) PROCDEC() | FUNDEC() }

void VARDEC(): {} { (EXP() (<AND> EXP())* "," <ID> ( <AND>  <ID>)*  (<BEGIN> | <BEGINS>))
                    | (<FROM> EXP() "," <ID>  <BEGINS>) }
void CONSTDEC(): {} { EXP() "," <ID>  <MUST>  <BE> }
void PROCDEC(): {} { BLOCK() <GIVEN> PARAMS() ","  <ID>  <DOES> }
void FUNDEC(): {} { BLOCK() <GIVEN> PARAMS() ","  <ID>  <GIVES> }
void PARAMS(): {} { <NOTHING> | (<ID> (  <AND>  <ID>)*) }
void ASSIGNMENT(): {} { EXP() (<AND> EXP())* ","  <ID> (  <AND>  <ID>)*  (<BECOME> | <BECOMES>) }
void PRINTSTMT(): {} { EXP() <YOU>  <PRINT> }
void RETURNSTMT(): {} { <GIVEBACK> EXP() <YOU>  <MUST> }
void CONDITIONAL(): {} { BLOCK() <IF> EXP() (LOOKAHEAD (3) (<BR>)+ (LOOKAHEAD (<ELSE>) <ELSE> BLOCK() <IF> EXP() (<BR>)+)* <ELSE> BLOCK())? }
void TIMESLOOP(): {} { BLOCK() EXP() <TIMES> }
void FORLOOP(): {} { BLOCK() <AS>  <THROUGH> RANGE() <ID>  <RUNS> }
void WHILELOOP(): {} { BLOCK() <WHILE> EXP() }
void PROCCALL(): {} { FUNCALL() }
void BLOCK(): {} { LOOKAHEAD(12) ("{" (STMT())+ "}") | ("{" (STMT() (<BR>)+)+ "}") }
void EXP(): {} { LOOKAHEAD("(" EXP() "|") ("(" EXP() "|" EXP() ")") | EXP1() }
void EXP1(): {} { LOOKAHEAD(3)("(" EXP1() "&" EXP1() ")") | EXP2() }
void EXP2(): {} { LOOKAHEAD (2) ("(" RELOP() EXP2() EXP2() <IS> ")") | LOOKAHEAD (2) ("(" ARITHOP() EXP2() (EXP2())+ ")") | EXP3() }
void EXP3(): {} { LOOKAHEAD (3)("(" ( UNARYOP() )? EXP3() ")") | EXP4() }
void EXP4(): {} { LOOKAHEAD (3)("(" EXP4() <HMMQ> EXP4() <HMM> EXP4() ")") | EXP5() }
void EXP5(): {} { ARRAY() | LIT() | LOOKAHEAD (2) ARRAYLOOKUP() | LOOKAHEAD (2) OBJECT() | FUNCALL() | <ID>}
void LIT(): {} { <NUMLIT> | <STRLIT> }
void ARRAY(): {} { ("[" (":" (<BR>)* EXP() ("," (<BR>)* EXP())* (<BR>)*)? "]") }
void ARRAYLOOKUP(): {} { <ID> "[" <NUMLIT> (":" <NUMLIT>)? "]" }
void OBJECT(): {} { "{" (":" <ID> EXP())* "}" (<TO>  <BE>  <ID>)? ","  (<ID> )? <TRAINING>  <BEGINS> }
void ANONFUN(): {} { BLOCK() <GIVEN> ARGS() }
void FUNCALL(): {} { (<ID>".")?"("ARGS()")"(<ID> | ANONFUN()) }
void RELOP(): {} { "<" | "<=" | "=" | "!=" | ">=" | ">" }
void ARITHOP(): {} { "*" | "/" | "+" | "-" | "^" }
void UNARYOP(): {} { "!" }

void ARGS(): {} { ARGS1() (LOOKAHEAD (2) "," ARGS1())* }
void ARGS1(): {} { EXP5() }
void RANGE(): {} { (LOOKAHEAD (2) ( LOOKAHEAD (2) <NUMLIT> | LOOKAHEAD (2) <ID> | FUNCALL()) (LOOKAHEAD (2) <TO> | <THROUGH>))? (LOOKAHEAD (2) <NUMLIT> | LOOKAHEAD (2) <ID> | FUNCALL()) (<BY> EXP3())? }
