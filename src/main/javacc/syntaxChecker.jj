PARSER_BEGIN(Parser)

package edu.lmu.cs.xlg.yoda.syntax;

import java.util.List;
import java.util.ArrayList;
import java.io.Reader;
import edu.lmu.cs.xlg.util.Log;
import edu.lmu.cs.xlg.yoda.entities.*;

public class Parser {
    /**
     * Returns the result of parsing the Yoda program on the given Reader.
     */
    public Script parse(Log log) {
        try {
            return SCRIPT();
        } catch (TokenMgrError e) {
            log.exception(e);
            return null;
        } catch (ParseException e) {
            log.exception(e);
            return null;
        }
    }
}

PARSER_END(Parser)

SKIP: { " " | "\t"
    | <"<(-_-)>" (~["\n","\r"])*>
}

TOKEN: {
    "(" | ")" | "{" | "}" | "+" | "*" | ":=" | "|" | "&" | "," | ":" | "."
    | <AND: ("and")>
    | <AS: ("as")>
    | <BE: ("be")>
    | <BECOME: ("become")>
    | <BECOMES: ("becomes")>
    | <BEGIN: ("begin")>
    | <BEGINS: ("begins")>
    | <BY: ("by")>
    | <DOES: ("does")>
    | <ELSE: ("else")>
    | <FALSE: ("false")>
    | <FROM: ("from")>
    | <GET: ("get")>
    | <GIVEBACK: ("give back")>
    | <GIVEN: ("given")>
    | <GIVES: ("gives")>
    | <HMM: ("hmm")>
    | <HMMQ: ("hmm?")>
    | <IF: ("if")>
    | <IS: ("is?")>
    | <MUST: ("must")>
    | <NOTHING: ("nothing")>
    | <ON: ("on")>
    | <PERHAPS: ("perhaps")>
    | <PRINT: ("print")>
    | <RUNS: ("runs")>
    | <TIMES: ("times")>
    | <THROUGH: ("through")>
    | <TO: ("to")>
    | <TRAINING: ("training")>
    | <TRUE: ("true")>
    | <WHILE: ("while")>
    | <YOU: ("you")>
    | <ID: ((["a"-"z"] | ["A"-"Z"])+(["-"] | ["_"] | ["a"-"z"] | ["A"-"Z"] | ["0"-"9"])*)>
    | <NUMLIT: ((("0x")(["0"-"9"] | ["A"-"F"] | ["a"-"f"])+("."(["0"-"9"] | ["A"-"F"] | ["a"-"f"])*)?)
               | (("0b")(["0"-"1"])+("."(["0"-"1"])*)?)
               | (("0o")(["0"-"7"])+("."(["0"-"7"])*)?)
               | ((["0"-"9"])+("."(["0"-"9"])*)?))>
    | <STRLIT: ("\"" (~["\""] | ("\\\""))* "\"") >
    | <BR: ("\n" | "\r" | "\r\n" )>

}


Script SCRIPT(): {
    List<Statement> statements = new ArrayList<Statement>();
    Statement s;
}
{
    (<BR>)*

    s = STMT() {statements.add(s);}
    (LOOKAHEAD((<BR>)+ STMT()) (<BR>)+ s = STMT() {statements.add(s);})*

    (<BR>)* <EOF>
    {return new Script(statements);}
}

Statement STMT(): {
    Statement s;
    Expression e;
}
{
   ( LOOKAHEAD(BLOCK() <WHILE>) s = WHILELOOP()
   | LOOKAHEAD(BLOCK() <AS>) s = FORLOOP()
   | LOOKAHEAD(BLOCK() <IF>) s = CONDITIONAL()
   | LOOKAHEAD(BLOCK() EXP()) s = TIMESLOOP()
   | LOOKAHEAD(BLOCK() <GIVEN> PARAMS() "," <ID> <DOES>) s = PROCDEC()
   | LOOKAHEAD(BLOCK() <GIVEN> PARAMS() "," <ID> <GIVES>) s = FUNDEC()
   | LOOKAHEAD(EXP() <YOU> <PRINT>) s = PRINTSTMT()
   | LOOKAHEAD(EXP() "," <ID> <MUST> <BE>) s = CONSTDEC()
   | LOOKAHEAD(<FROM> | (EXP() (<AND> EXP())*  "," <ID> ( <AND> <ID>)* (<BEGIN> | <BEGINS>))) s = VARDEC()
   | LOOKAHEAD(EXP() (<AND> EXP())* "," <ID> ( <AND> <ID>)* (<BECOME> | <BECOMES>)) s = ASSIGNMENT()
   | LOOKAHEAD(<GIVEBACK>) s = RETURNSTMT()
   | LOOKAHEAD(FUNCALL()) e = PROCCALL() {return new ExpressionStatement(e);}
   | e = EXP() {return new ExpressionStatement(e);}
   )
   {return s;}
}

List<Expression> ARGS(): {
    Expression e;
    List<Expression> args = new ArrayList<Expression>();
}
{
    e = EXP() {args.add(e);} (<AND> e = EXP() {args.add(e);})*
    {return args;}
}

ArrayConstructor ARRAY():{
    Expression e;
    List<Expression> es = new ArrayList<Expression>();
}
{
    ("[" ( (<BR>)* e = EXP() {es.add(e);}) ("," (<BR>)* e = EXP() {es.add(e);} (<BR>)*)* "]")
    {return new ArrayConstructor(es);}
}

Statement ASSIGNMENT(): {
    List<Expression> targets = new ArrayList<Expression>();
    List<Expression> sources = new ArrayList<Expression>();
    Token t;
    Expression e;
    IdentifierExpression i;
}
{
    ((e = EXP(){sources.add(e);} (<AND> e = EXP() {sources.add(e);})*
        ","  t = <ID> {i = new IdentifierExpression(t.image);
        targets.add(i);}(<AND> t = <ID> {targets.add(i);})*  (<BECOME> | <BECOMES>)))
    {return new AssignmentStatement(targets, sources);}
}

Block BLOCK(): {
    List<Statement> statements = new ArrayList<Statement>();
    Statement s;
}
{
    "{"
    (<BR>)*

    s = STMT() {statements.add(s);}
    (LOOKAHEAD((<BR>)+ STMT()) (<BR>)+ STMT() {statements.add(s);})*

    (<BR>)* "}"
    {return new Block(statements);}
}

Statement CONDITIONAL(): {
    List<ConditionalStatement.Arm> arms = new ArrayList<ConditionalStatement.Arm>();
    Expression e;
    Block b;
    Block elseBlock = null;
}
{
    (b = BLOCK() <IF>
        e = EXP() {arms.add(new ConditionalStatement.Arm(e, b));}
    (LOOKAHEAD((<BR>)+ <ELSE> BLOCK() <IF>) (<BR>)+ <ELSE> b = BLOCK() <IF> e = EXP()
        {arms.add(new ConditionalStatement.Arm(e, b));})*
    (LOOKAHEAD((<BR>)+ <ELSE>)(<BR>)+ <ELSE> elseBlock = BLOCK() )?)

    {return new ConditionalStatement(arms, elseBlock);}
}

DecStatement CONSTDEC(): {
    Token t;
    Expression e;
    boolean constant = true;
}
{
    ((e = EXP() ) "," (t = <ID>)  <MUST>  <BE>)
    {
        return new DecStatement(t.image, e, constant);
    }
}

Expression EXP(): {
    String op = "+";
    Expression e1 = null;
    Expression e2 = null;
    Expression returnedExp = null;
    List<Expression> exps = new ArrayList<Expression>();
    Expression condition = null;
    Token t = null;
}
{
    (
      ( "(" (( op = RELOP() e1 = EXP() e2 = EXP() <IS> {returnedExp = new RelationalExpression(e1, op, e2);} )
          | (op = ARITHOP() e1 = EXP() {exps.add(e1);}
               (e2 = EXP() {exps.add(e2);})
               {returnedExp = new ArbitraryArityExpression(op, exps);}
            )
          | returnedExp = FUNCALL()
          | (<PERHAPS> condition = EXP() <HMMQ> e1 = EXP() <HMM> e2 = EXP()
               {returnedExp = new TernaryExpression(condition, e1, e2);})
          | (op = UNARYOP() e1 = EXP() {returnedExp = new UnaryExpression(op, e1);})
             ) ")" )

   | returnedExp = ARRAY()
   | returnedExp = LIT()
// TODO Array lookup not defined properly!
   | LOOKAHEAD(<ID> "[") t = <ID> {returnedExp = new IdentifierExpression(t.image);} "[" ("-")? <NUMLIT> "]"
   | t = <ID> {returnedExp = new IdentifierExpression(t.image);})
   {return returnedExp;}
}

Statement FORLOOP(): {
    Block b;
    Token t;
    Expression condition = null;
    Expression step = new NumberLiteral("1");
    Range r = null;
}
{
    b = BLOCK() <AS>  <THROUGH> (LOOKAHEAD( (<NUMLIT> | <ID> | FUNCALL() ) <TO>) r = RANGE()
                                             | condition = EXP() ) (<BY> step = EXP())? t = <ID>  <RUNS>
    {
        if(condition == null) {
            return new RangeLoop(new Variable(t.image), r, step, b);
        } else {
            return new ConditionalLoop(new Variable(t.image), condition, step, b);
        }
    }
}

FunctionCall FUNCALL(): {
    Token functionName = null;
    List<Expression> args = new ArrayList<Expression>();
}
{
    (<ON> (<NOTHING> | args = ARGS()) (functionName = <ID>))
    {
        return new FunctionCall(new IdentifierExpression(functionName.image), args);
    }
}

Function FUNDEC(): {
    Token t;
    Block b;
    List<Variable> params = new ArrayList<Variable>();
}
{
    ((b = BLOCK()) <GIVEN> (PARAMS(params)) ","  (t = <ID>)  <GIVES>)
    {return new Function(t.image, params, b);}
}

void PARAMS(List<Variable> params): {
    Token t;
}
{
    (<NOTHING> | (t = <ID> {params.add(new Variable(t.image, null, false, false));}
     (  <AND>  t = <ID> {params.add(new Variable(t.image, null, false, false));})*))
}

Statement PRINTSTMT(): {
    Expression e;
}
{
    (e = EXP() <YOU> <PRINT>)
    {return new PrintStatement(e);}
}

Procedure PROCDEC(): {
    Token t;
    Block b;
    List<Variable> params = new ArrayList<Variable>();
}
{
    ((b = BLOCK()) <GIVEN> (PARAMS(params)) ","  (t = <ID>)  <DOES>)
    {return new Procedure(t.image, params, b);}
}

Range RANGE(): {
    Expression l = null;
    Expression h = null;
    Token t;
}
{
    (t = <NUMLIT> {l = new NumberLiteral(t.image);}
    |t = <ID> {l = new IdentifierExpression(t.image);}
    | FUNCALL())
    <TO> (t = <NUMLIT> {h = new NumberLiteral(t.image);}
    | t = <ID> {h = new IdentifierExpression(t.image);}
    | FUNCALL())
    {return new Range(l, h);}
}

Statement RETURNSTMT(): {
    Expression e = null;
}
{
    <GIVEBACK>  e = EXP() <YOU> <MUST>
    {return new ReturnStatement(e);}
}

Statement TIMESLOOP(): {
    Block b;
    Expression e;
}
{
    (b = BLOCK() e = EXP() <TIMES>)
    {return new TimesLoop(e, b);}
}

Statement WHILELOOP(): {
    Block b;
    Expression e;
}
{
    b = BLOCK() <WHILE> e = EXP()
    {return new WhileLoop(e, b);}
}

FunctionCall PROCCALL(): {
    FunctionCall f;
}
{
    f = FUNCALL()
    {return f;}
}

DecStatement VARDEC(): {
    List<String> tokens = new ArrayList<String>();
    List<Expression> exps = new ArrayList<Expression>();
    Token t;
    Expression e;
}
{
    (
      (e = EXP()) {exps.add(e);}
      (<AND> (e = EXP()) {exps.add(e);})*
      ","
      (t = <ID>) {tokens.add(t.image);}
      (<AND> (t = <ID>) {tokens.add(t.image);})*
      (<BEGIN> | <BEGINS>)
    )

    {
      return new DecStatement(tokens, exps);
    }
}

Literal LIT(): {
    Token t;
}
{
  t = <TRUE>
  {return BooleanLiteral.TRUE;}
  |
  t = <FALSE>
  {return BooleanLiteral.FALSE;}
  |
  t = <NUMLIT>
  {return new NumberLiteral(t.image);}
  |
  t = <STRLIT>
  {return new StringLiteral(t.image);}
}

String RELOP(): {
    Token s;
}

{   (s = "<"
    | s = "<="
    | s = "="
    | s = "!="
    | s = ">="
    | s = ">")
    {return s.image;}
}
String ARITHOP(): {
    Token s;
}
{
    (s = "*"
    | s = "/"
    | s = "+"
    | s = "-"
    | s = "%"
    | s = "^"
    | s = "|"
    | s = "&")
    {return s.image;}
}

String UNARYOP(): {
    Token t;
}
{
    t = "!"
    {return t.image;}
}

String INCOP(): {
    Token s;
}
{
    s = "++"
    | s = "--"
    {return s.image;}
}


